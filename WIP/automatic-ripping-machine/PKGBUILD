# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=automatic-ripping-machine
pkgver=2.6.18
pkgrel=2
pkgdesc="Automatic Ripping Machine (ARM) scripts for CD, DVD and Blu-Ray"
arch=(x86_64)
url="https://b3n.org/automatic-ripping-machine/"
license=(MIT)
depends=(
        abcde
        alsa-lib
        at
        atomicparsley
        avahi
        ffmpeg
        flac
        git
        glyr
        handbrake-cli
        imagemagick
        jre-openjdk-headless
        libdvdcss
        lsdvd
        lsscsi
        makemkv-cli
        net-tools
        util-linux #eject

         # python requirements.txt
        python-markdown
        python-pycurl
        python-pydvdid #AUR
        python-requests
        python-urllib3
        python-xmltodict
        python-pyudev
        python-yaml
        python-flask
        python-flask-wtf
        python-flask-sqlalchemy
        python-flask-migrate
        python-flask-cors
        python-psutil
        python-robobrowser-git #my local pkgbuild
        #python-tinydownload #AUR -git
        python-netifaces
        python-flask-login
        python-apprise #AUR
        python-bcrypt
        python-musicbrainzngs
        python-discid
        python-prettytable
        )
makedepends=(git)
optdepends=('handbrake-full-cli: handbrake with all features')
backup=(
    opt/arm/setup/apprise.yaml
    opt/arm/setup/arm.yaml
    )
#install=
options=(!strip)
source=("git+https://github.com/automatic-ripping-machine/automatic-ripping-machine.git#tag=v${pkgver}")
sha256sums=('SKIP')

package() {
  install -d ${pkgdir}/etc/arm/config
  install -d ${pkgdir}/etc/rsyslog.d/
  install -d ${pkgdir}/opt/arm/
  install -d ${pkgdir}/opt/arm/scripts
  install -d ${pkgdir}/opt/arm/setup
  install -d ${pkgdir}/usr/lib/udev/rules.d/
  install -d ${pkgdir}/usr/lib/systemd/system/
  install -d ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install -d ${pkgdir}/usr/share/licenses/${pkgname}

  cd automatic-ripping-machine
  cp -r arm              ${pkgdir}/opt/arm/
  cp -r .git             ${pkgdir}/opt/arm/
  install favicon.ico -t ${pkgdir}/opt/arm/
  install setup.cfg   -t ${pkgdir}/opt/arm/
  install VERSION     -t ${pkgdir}/opt/arm/

  install LICENSE      -t ${pkgdir}/usr/share/licenses/${pkgname}/
  install README.md    -t ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install CHANGELOG.md -t ${pkgdir}/usr/share/doc/automatic-ripping-machine

  install docs/README-OMDBAPI.md  -t ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install docs/README-TMDBAPI.txt -t ${pkgdir}/usr/share/doc/automatic-ripping-machine

  cd scripts
  cp -r thickclient ${pkgdir}/opt/arm/scripts
  install update_key.sh -t ${pkgdir}/opt/arm/scripts
  cd ..

  cd setup
  install 30-arm.conf        -t ${pkgdir}/etc/rsyslog.d/
  install 30-arm.conf        -t ${pkgdir}/opt/arm/setup
  install 51-automedia.rules -t ${pkgdir}/usr/lib/udev/rules.d/
  #install apprise.yaml       -t ${pkgdir}/etc/arm/config
  install apprise.yaml       -t ${pkgdir}/opt/arm/setup
  #install arm.yaml           -t ${pkgdir}/etc/arm/config
  install arm.yaml           -t ${pkgdir}/opt/arm/setup
  install armui.service      -t ${pkgdir}/usr/lib/systemd/system/
  install .abcde.conf        -t ${pkgdir}/opt/arm/setup
  install .abcde.conf        -t ${pkgdir}/etc
  # link to the new install location so runui.py doesn't break
  ln -sf /etc/.abcde.conf            ${pkgdir}/etc/arm/config/abcde.conf
  ln -sf /opt/arm/setup/apprise.yaml ${pkgdir}/etc/arm/config/apprise.yaml
  ln -sf /opt/arm/setup/arm.yaml     ${pkgdir}/etc/arm/config/arm.yaml
  cd ..

  chown arm:arm "${pkgdir}/etc/.abcde.conf"
  chown arm:arm "${pkgdir}/etc/rsyslog.d/30-arm.conf"
  chown -vR arm:arm "${pkgdir}/etc/arm"
  chown -vR arm:arm "${pkgdir}/opt/arm"
}
