# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=automatic-ripping-machine-git
pkgver=2.6.18.r10.gdca884b
pkgrel=1
pkgdesc="Automatic Ripping Machine (ARM) scripts for CD, DVD and Blu-Ray"
arch=(x86_64)
url="https://b3n.org/automatic-ripping-machine/"
license=(MIT)
depends=(
        abcde
        at
        flac
        glyr
        handbrake-cli
        imagemagick
        jre-openjdk-headless
        libdvdcss
        makemkv-cli

         # python requirements.txt
        python-markdown
        python-pycurl
        python-pydvdid #AUR
        python-requests
        python-urllib3
        python-xmltodict
        python-pyudev
        python-yaml
        python-flask
        python-flask-wtf
        python-flask-sqlalchemy
        python-flask-migrate
        python-flask-cors
        python-psutil
        #robobrowser #misssing
        #python-tinydownload #AUR -git
        python-netifaces
        python-flask-login
        python-apprise #AUR
        python-bcrypt
        python-musicbrainzngs
        python-discid
        python-prettytable
        )
makedepends=(git)
optdepends=('handbrake-full-cli: handbrake with all features')
provides=(automatic-ripping-machine)
conflicts=(automatic-ripping-machine)
#backup=()
#install=
source=("git+https://github.com/automatic-ripping-machine/automatic-ripping-machine.git")
sha256sums=('SKIP')

pkgver() {
  cd automatic-ripping-machine
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  install -d ${pkgdir}/etc
  install -d ${pkgdir}/opt/arm/
  install -d ${pkgdir}/opt/arm/{scripts,setup}
  install -d ${pkgdir}/usr/lib/udev/rules.d/
  install -d ${pkgdir}/usr/lib/systemd/system/
  install -d ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install -d ${pkgdir}/usr/share/licenses/${pkgname}

  cd automatic-ripping-machine
  cp -r arm              ${pkgdir}/opt/arm/
  install favicon.ico -t ${pkgdir}/opt/arm/
  install setup.cfg   -t ${pkgdir}/opt/arm/

  install LICENSE      -t ${pkgdir}/usr/share/licenses/${pkgname}/
  install README.md    -t ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install CHANGELOG.md -t ${pkgdir}/usr/share/doc/automatic-ripping-machine

  install docs/README-OMDBAPI.md  -t ${pkgdir}/usr/share/doc/automatic-ripping-machine
  install docs/README-TMDBAPI.txt -t ${pkgdir}/usr/share/doc/automatic-ripping-machine

  #cp -r scripts ${pkgdir}/opt/arm/
  #rm -R ${pkgdir}/opt/arm/scripts/docker
  #rm -R ${pkgdir}/opt/arm/scripts/installers

  cd scripts
  cp -r thickclient ${pkgdir}/opt/arm/scripts
  install update_key.sh -t ${pkgdir}/opt/arm/scripts
  cd ..

  cd setup
  install 30-arm.conf        -t ${pkgdir}/etc
  install 51-automedia.rules -t ${pkgdir}/usr/lib/udev/rules.d/
  install apprise.yaml       -t ${pkgdir}/opt/arm/setup
  install arm.yaml           -t ${pkgdir}/opt/arm/setup
  install armui.service      -t ${pkgdir}/usr/lib/systemd/system/

  #cp -r setup ${pkgdir}/opt/arm/
  #mv ${pkgdir}/opt/arm/setup/51-automedia.rules ${pkgdir}/usr/lib/udev/rules.d/
  #mv ${pkgdir}/opt/arm/setup/armui.service -t ${pkgdir}/usr/lib/systemd/system/
  #rm ${pkgdir}/opt/arm/setup/51-docker-arm.rules
}
