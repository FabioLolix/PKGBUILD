# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=newbreeze-git
pkgver=3.0.0.rc8.r0.g81b93d2
pkgrel=1
pkgdesc="File Manager based on Qt"
arch=('x86_64')
url="https://github.com/marcusbritanicus/NewBreeze"
license=(GPL3)
depends=('qt5-base' 'poppler-qt5')
makedepends=('git' 'qscintilla-qt5' 'djvulibre')
optdepends=('qscintilla-qt5')
provides=('newbreeze')
conflicts=('newbreeze')
source=("${pkgname}::git+https://github.com/marcusbritanicus/NewBreeze.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
   git describe --long --tags | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${srcdir}/${pkgname}
  qmake-qt5 NewBreeze.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}
  make install INSTALL_ROOT="${pkgdir}/"
}
