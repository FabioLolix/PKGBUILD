# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=open-horizon-git
pkgver=r772.0403769
pkgrel=1
pkgdesc="Open source implementation of Ace Combat: Assault Horizonâ€˜s render and physics"
arch=(x86_64)
url="http://zxstudio.org/blog/author/razgriz/"
license=(MIT)
depends=()
makedepends=(cmake lua51 git glfw)
optdepends=()
provides=()
conflicts=()
replaces=()
options=()
#install=
#changelog=
source=("git+https://github.com/undefined-darkness/open-horizon.git"
        "git+https://bitbucket.org/nyan_developer/nya-engine.git#branch=renderapi"
        "git+https://github.com/antze-k/miso"
        "git+https://github.com/kuba--/zip")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  install -d {convert-locations-cmake,open-horizon-cmake}

  git submodule init
  git config 'submodule.deps/nya-engine.url' "${srcdir}/nya-engine"
  git config 'submodule.deps/miso.url' "${srcdir}/miso"
  git config 'submodule.deps/zip.url' "${srcdir}/zip"
  git submodule update
}

build() {
  cd "$srcdir/${pkgname%-git}/open-horizon-cmake"
  cmake ..
  make open_horizon

  cd "$srcdir/${pkgname%-git}/convert-locations-cmake"
  cmake ../convert_locations 
  make convert_locations
}

package() {
  cd "$srcdir/${pkgname%-git}/convert-locations-cmake"
  make DESTDIR="$pkgdir" install
}
