# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Maintainer: E5ten

pkgbase=lumina-desktop-utils
pkgname=(lumina-icon-theme lumina-archiver lumina-fileinfo lumina-fm lumina-mediaplayer
         lumina-screenshot lumina-textedit lumina-open) #lumina-screencast lumina-terminal
pkgver=1.6.0
pkgrel=1
pkgdesc="Desktop applications from Lumina project"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://lumina-desktop.org/"
license=(BSD)
depends=(qt5-base)
makedepends=(qt5-svg qt5-tools qt5-x11extras qt5-multimedia poppler-qt5)
conflicts=(lumina-desktop)
source=("lumina-desktop-${pkgver}.tar.gz::https://github.com/lumina-desktop/lumina/archive/v${pkgver}.tar.gz")
sha512sums=('e923961568ab9cc32206386066daef8cd1fd2db2d5c3e260e5c1da1dcda39a516576418f607cefbe5a1c47d8684591d0e595fb344e7d6aa2859fc3b326e73e30')

_lumina-build() {
    cd "${srcdir}/lumina-${pkgver}/src-qt5/${_location}/$pkgname"
    qmake QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" LIBPREFIX=/usr/lib QT5LIBDIR=/usr/lib/qt CONFIG+=WITH_I18N L_MANDIR=/usr/share/man L_ETCDIR="/etc"
    make
    make INSTALL_ROOT="${pkgdir}" install
    install -D "${srcdir}/lumina-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lumina-icon-theme() {
  pkgname=lumina-icon-theme
  pkgdesc="Icon theme for Lumina desktop applications"
  arch=(any)
  depends=()
  options=(!strip)

    cd "${srcdir}/lumina-${pkgver}/icon-theme"
    qmake QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" LIBPREFIX=/usr/lib QT5LIBDIR=/usr/lib/qt CONFIG+=WITH_I18N L_MANDIR=/usr/share/man L_ETCDIR="/etc"
    make
    make INSTALL_ROOT="${pkgdir}" install
    install -D "${srcdir}/lumina-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lumina-archiver() {
  pkgname=lumina-archiver
  pkgdesc="Archivier for the lumina desktop environment"
  depends+=(lumina-icon-theme)

  _location=desktop-utils
  _lumina-build
}

package_lumina-fileinfo() {
  pkgname=lumina-fileinfo
  pkgdesc="File property manager for the lumina desktop environment"
  depends+=(qt5-multimedia lumina-icon-theme)

  _location=desktop-utils
  _lumina-build
}

package_lumina-fm() {
  pkgname=lumina-fm
  pkgdesc="File manager for the lumina desktop environment"
  depends+=(qt5-x11extras qt5-multimedia lumina-icon-theme lumina-open lumina-fileinfo)

  _location=desktop-utils
  _lumina-build
}

package_lumina-mediaplayer() {
  pkgname=lumina-mediaplayer
  pkgdesc="Mediaplayer for the lumina desktop environment"
  depends+=(qt5-x11extras qt5-multimedia lumina-icon-theme)

  _location=desktop-utils
  _lumina-build
}

package_lumina-screenshot() {
  pkgname=lumina-screenshot
  pkgdesc="Screenshot utility for the lumina desktop environment"
  depends+=(qt5-x11extras lumina-icon-theme)

  _location=desktop-utils
  _lumina-build
}

package_lumina-textedit() {
  pkgname=lumina-textedit
  pkgdesc="Text editor for the lumina desktop environment"
  depends+=(lumina-icon-theme)

  _location=desktop-utils
  _lumina-build
}

package_lumina-screencast() {
  pkgname=lumina-screencast
  pkgdesc="(experimental)"
  depends+=(lumina-icon-theme)

  _location=experimental
  _lumina-build
}

package_lumina-terminal() {
  pkgname=lumina-terminal
  pkgdesc="(experimental)"
  depends+=(lumina-icon-theme)

  _location=experimental
  _lumina-build
}

package_lumina-open() {
  pkgname=lumina-open
  pkgdesc="Application launcher for the lumina desktop environment"

  _location=core
  _lumina-build
}
