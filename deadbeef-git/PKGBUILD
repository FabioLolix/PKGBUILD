# Maintainer: Soukyuu <chrno-sphered at hotmail dot com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=deadbeef-git
pkgver=1.9.6.r297.g76b765ecd
pkgrel=1
pkgdesc="Modular GTK audio player for GNU/Linux"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://deadbeef.sourceforge.io/"
license=(GPL2 LGPL2.1 ZLIB)
depends=(gtk3 alsa-lib jansson libdispatch)
makedepends=(libvorbis libmad flac curl imlib2 wavpack libsndfile libcdio libcddb
             libx11 faad2 zlib intltool libpipewire libpulse libzip libsamplerate
             yasm ffmpeg clang git wget)
optdepends=('alsa-oss: for OSS output plugin'
            'cdparanoia: for cd audio plugin'
            'curl: for last.fm, vfs_curl (shoutcast/icecast), artwork plugins'
            'dbus: for notification daemon support (OSD current song notifications)'
            'faad2: for AAC plugin'
            'ffmpeg: for ffmpeg plugin'
            'flac: for flac plugin'
            'imlib2: for artwork plugin'
            'libcddb: for cd audio plugin'
            'libcdio: for cd audio plugin'
            'libice: optional dependency for gtkui session client support'
            'libmad: for mp3 plugin (mpeg1,2 layers1,2,3)'
            'libogg: for ogg vorbis plugin'
            'libpipewire: for pipewire plugin'
            'libsamplerate: for dsp_libsrc plugin (resampler)'
            'libsidplay: for SID player plugin'
            'libsm: optional dependency for gtkui session client support'
            'libsndfile: for sndfile plugin'
            'libvorbis: for ogg vorbis plugin'
            'libx11: for global hotkeys plugin'
            'libzip: for vfs_zip plugin'
            'mpg123: for MP1/MP2/MP3 playback'
            'opusfile: for opus plugin'
            'pulseaudio: for PulseAudio output plugin'
            'wavpack: for wavpack plugin'
            'yasm: required to build assembly portions of ffap plugin'
            'zlib: for Audio Overload plugin (psf, psf2, etc), GME (for vgz)')
conflicts=(deadbeef)
provides=(deadbeef)
source=("git+https://github.com/DeaDBeeF-Player/deadbeef.git"
        "git+https://github.com/DeaDBeeF-Player/mp4p.git"
        "deadbeef-googletest::git+https://github.com/DeaDBeeF-Player/googletest.git"
        "git+https://github.com/DeaDBeeF-Player/ddb_output_pw.git"
        "git+https://github.com/DeaDBeeF-Player/ddb_dsp_libretro.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/deadbeef"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/deadbeef"
  git submodule init
  git config submodule.osx/deps.update                  none
  git config submodule.external/apbuild.update          none
  git config submodule.external/mp4p.url                "${srcdir}/mp4p"
  git config submodule.external/googletest.url          "${srcdir}/deadbeef-googletest"
  git config submodule.external/ddb_output_pw.url       "${srcdir}/ddb_output_pw"
  git config submodule.external/ddb_dsp_libretro.url    "${srcdir}/ddb_dsp_libretro"
  git -c protocol.file.allow=always submodule update

  ./autogen.sh
}

build() {
  cd "${srcdir}/deadbeef"
  export CC=clang CXX=clang++
  ./configure --prefix=/usr --disable-gtk2
  make
}

check() {
  cd "${srcdir}/deadbeef"
  ./travis/download-linux-static-deps.sh
  ./scripts/test.sh
}

package() {
  cd "${srcdir}/deadbeef"
  make DESTDIR="$pkgdir" install
  install -D COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
