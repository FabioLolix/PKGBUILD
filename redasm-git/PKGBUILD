# Maintainer: Martin Sandsmark <martin.sandsmark@kde.org>

pkgname=redasm-git
pkgver=2.1.1.r0.g8848416
pkgrel=1
url="https://github.com/REDasmOrg/REDasm"
arch=(i686 x86_64)
pkgdesc="The OpenSource Disassembler"
license=(GPL3)
depends=(qt5-base)
makedepends=(git cmake)
conflicts=(redasm)
provides=(redasm)
source=("git+https://github.com/REDasmOrg/REDasm.git"
        "git+https://github.com/Dax89/QHexView.git"
        "git+https://github.com/aquynh/capstone.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd REDasm
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd REDasm
  git submodule init
  git config submodule.widgets/QHexView.url "$srcdir/QHexView"
  git submodule update
  mkdir -p build

  cd LibREDasm
  git config submodule.depends/capstone.url "$srcdir/capstone"
  git submodule update
}

build() {
  cd REDasm/build
  cmake ../REDasm -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd REDasm/build
  install -D -m755 REDasm ${pkgdir}/usr/bin/REDasm
  install -D -m755 LibREDasm.so ${pkgdir}/usr/lib/LibREDasm.so
  install -D -m644 ../README.md ${pkgdir}/usr/share/doc/${pkgname}/README
  install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
