# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=flacon-git
pkgver=11.1.0.r14.g175a323
pkgrel=1
pkgdesc="Extracts individual tracks from one big audio file containing the entire album of music and saves them as separate audio files. (Git Version)"
arch=('x86_64')
url='http://flacon.github.io'
license=('LGPL2.1')
depends=('qt5-base'
         'uchardet'
         'taglib'
         'hicolor-icon-theme'
         'desktop-file-utils'
         )
makedepends=('git'
             'cmake'
             'qt5-tools'
             )
optdepends=(
            'alacenc: For ALAC support'
            'faac: For AAC support'
            'flac: For FLAC support'
            'lame: For MP3 support'
            'mac: For APE support'
            'opus-tools: For OPUS support'
            'sox: For SoX support'
            'ttaenc: For TrueAudio support'
            'vorbis-tools: For OGG support'
            'wavpack: For WavPack support'
            )
conflicts=('flacon')
provides=('flacon')
source=('git+https://github.com/flacon/flacon.git')
sha256sums=('SKIP')

pkgver() {
  cd flacon
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -S flacon -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=Yes

  cmake --build build
}

check() {
  cd build
  #ctest --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
}
