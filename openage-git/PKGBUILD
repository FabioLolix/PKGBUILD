# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: fzerorubigd <fzero@rubi.gd>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=openage-git
pkgver=0.5.1.r4.g4773e70ff
pkgrel=2.1
pkgdesc="A free (as in freedom) clone of the Age of Empires II engine"
arch=(x86_64 i686 pentium4 armv7h aarch64)
url="https://openage.sft.mx/"
license=(GPL3)
depends=(libepoxy opusfile sdl2 sdl2_image
         qt6-declarative vulkan-icd-loader
         libpng libogg nyan-lang
         python-pillow python-numpy python-toml python-mako
         python-pygments python-lz4

         # namcap implicit depends
         glibc gcc-libs python gperftools harfbuzz fontconfig freetype2
         qt6-base libglvnd opus ncurses)
makedepends=(git cmake cython doxygen vulkan-headers python-graphviz eigen toml11)
optdepends=('ttf-dejavu: for text display, but you can use any font')
provides=(openage)
conflicts=(openage)
source=("git+https://github.com/SFTtech/openage.git"
        "openage-001.patch::https://github.com/SFTtech/openage/pull/1570/commits/4002db5368e3a0f2908f810b20950bb2b8bb9b59.patch"
        #"openage-002.patch::https://github.com/SFTtech/openage/pull/1570/commits/c6a2ef9b760c903cf924f5947276395972f12eeb.patch"
        "openage-003.patch::https://github.com/SFTtech/openage/pull/1570/commits/52058a1d261cceb3db4c345248ba76d2bc3017eb.patch"

        # https://github.com/SFTtech/openage/pull/1574/commits
        "openage-d065eed63e387c19bc2bc5348bc18cffd56f826c.patch::https://github.com/SFTtech/openage/pull/1574/commits/d065eed63e387c19bc2bc5348bc18cffd56f826c.patch"
        "openage-297500e8d172ac2370c34b25e1f1ce42ac32cfe1.patch::https://github.com/SFTtech/openage/pull/1574/commits/297500e8d172ac2370c34b25e1f1ce42ac32cfe1.patch"
        "openage-f127bbb139f276429fb3fd06619bb87aec123ce5.patch::https://github.com/SFTtech/openage/pull/1574/commits/f127bbb139f276429fb3fd06619bb87aec123ce5.patch"
        "openage-2493d187b31d84a67e77d911e931716cecb2a9e5.patch::https://github.com/SFTtech/openage/pull/1574/commits/2493d187b31d84a67e77d911e931716cecb2a9e5.patch"
        "openage-fa596294cba7799144bac5b3d288442c9e5974ee.patch::https://github.com/SFTtech/openage/pull/1574/commits/fa596294cba7799144bac5b3d288442c9e5974ee.patch"
        "openage-f9abad3e14d158215238e629e5aa9e898b9ff992.patch::https://github.com/SFTtech/openage/pull/1574/commits/f9abad3e14d158215238e629e5aa9e898b9ff992.patch")
sha256sums=('SKIP'
            'db510135ba71b65ece5faf990d3b7d42831b96ddacfa582b8ceaafe11fce00e9'
            'b50039e9b5f2ac6aec59a6fd9b5143d3506256fcea49b549ed8627602b229d49'
            '60556486ad38077eb1c2f3ab4e72f4d01ebd56bce9b17e7502b79be442470c7b'
            'f8b10c8137911f45f471c05d6a41ea0b163effa33da2ff33cfab197449143a5e'
            'e683ce4b9600d5c9be7e307492257dba4c28a799c4b960dca8c5b30fa96ed1b8'
            '209a0378ed8b6f4ea3c05b0a58c44ac6f0978ae3486e821884f9975267e30680'
            'bc846610e8edd6908fa9b5baeeaec1505450ff22a4f4b5d3803768abf9ef81d4'
            'df0560bc2c1afbb223801ed25857f40c6a6a2b93d26fa74793d219866cff05ac')

prepare() {
  cd openage
  patch -Np1 -i ../openage-001.patch
  #patch -Np1 -i ../openage-002.patch
  patch -Np1 -i ../openage-003.patch

  patch -Np1 -i ../openage-d065eed63e387c19bc2bc5348bc18cffd56f826c.patch
  patch -Np1 -i ../openage-297500e8d172ac2370c34b25e1f1ce42ac32cfe1.patch
  patch -Np1 -i ../openage-f127bbb139f276429fb3fd06619bb87aec123ce5.patch
  patch -Np1 -i ../openage-2493d187b31d84a67e77d911e931716cecb2a9e5.patch
  patch -Np1 -i ../openage-fa596294cba7799144bac5b3d288442c9e5974ee.patch
  patch -Np1 -i ../openage-f9abad3e14d158215238e629e5aa9e898b9ff992.patch
}

pkgver() {
  cd openage
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "openage" -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=ON

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure

  cd build
  ./run.py test --run-all-tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
