# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libupnpp-git
pkgver=0.26.8.r0.g39ba0ff
pkgrel=1
pkgdesc="A C++ wrapper for libupnp - used with upmpdcli or upplay"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64 i686 armv6h armv7h aarch64)
license=(LGPL2.1)
depends=(glibc gcc-libs curl expat libnpupnp libnpupnp.so)
makedepends=(git meson)
provides=(libupnpp libupnpp.so)
conflicts=(libupnpp)
source=("git+https://framagit.org/medoc92/libupnpp.git")
sha256sums=('SKIP')

pkgver() {
  cd "libupnpp"
  git describe --long --tags | sed 's/^libupnpp.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  arch-meson libupnpp build
  meson compile -C build
}

package(){
  meson install -C build --destdir "$pkgdir"
}
