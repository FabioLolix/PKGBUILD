# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libupnpp-git
pkgver=0.22.4.r13.gd02751d
pkgrel=1
pkgdesc="A C++ wrapper for libupnp - used with upmpdcli or upplay"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64 i686 armv6h armv7h aarch64)
license=(LGPL2.1)
depends=(libnpupnp expat)
makedepends=(git)
provides=(libupnpp)
conflicts=(libupnpp)
source=("${pkgname%-git}::git+https://framagit.org/medoc92/libupnpp.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^libupnpp.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  ./autogen.sh
}

build(){
  cd "${pkgname%-git}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname%-git}"
  make DESTDIR="$pkgdir" install
}
