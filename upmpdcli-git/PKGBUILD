# Maintainer: AudioLinux  <audiolinux AT fastmail DOT fm>
# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Mansour Behabadi <mansour@oxplot.com>
# Contributor: Tilman Vatteroth <tilman.vatteroth@udo.edu>

pkgname=upmpdcli-git
pkgver=1.8.7.r23.g0bdb79f
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=(i686 x86_64 arm armv7h armv6h aarch64)
url="https://www.lesbonscomptes.com/upmpdcli/"
license=(GPL2)
depends=(python libmpdclient libupnpp libmicrohttpd jsoncpp)
makedepends=(git)
optdepends=(aspell-en id3lib sqlite
            'python-requests: enable search'
            'python-bottle: enable uprcl media server'
            'python-mutagen: enable uprcl media server'
            'python-waitress: enable uprcl media server')
install=upmpdcli.install
provides=(upmpdcli)
conflicts=(upmpdcli)
source=("git+https://framagit.org/medoc92/upmpdcli.git"
        'upmpdcli.service')
sha256sums=('SKIP'
            '53da5e4e40a1987e3c137270b315f8c0ba9521539c563e75e254f48449ce0ae9')
backup=('etc/upmpdcli.conf')

pkgver() {
  cd "upmpdcli"
  git describe --long --tags | sed 's/^upmpdcli.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "upmpdcli"
  ./autogen.sh
}

build() {
  cd "upmpdcli"
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  depends+=(libupnpp.so)
  cd "upmpdcli"
  make DESTDIR="${pkgdir}" install
  install -Dm644 systemd/upmpdcli.service "${pkgdir}"/usr/lib/systemd/system/upmpdcli.service
  sed '/\[Service\]/a User=upmpdcli' -i ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed -i '/^After/ s/$/ mpd.service/' ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  install -Dm644 "${srcdir}"/upmpdcli.service "${pkgdir}"/usr/lib/systemd/user/upmpdcli.service
  chmod 644 "${pkgdir}"/etc/upmpdcli.conf
}
