# Maintainer: Your Name <youremail@domain.com>

pkgname=utsushi-git
pkgver=r321.b296671
pkgrel=4
pkgdesc="Front-end for scanners and all-in-ones (Epson ImageScan v3)"
arch=(x86_64)
url="https://gitlab.com/utsushi/utsushi"
license=(GPL3)
depends=(glibc gcc-libs pangomm libusb boost-libs libjpeg-turbo libtiff libtool glibmm glib2 atkmm libsigc++ udev
         gtkmm imagemagick)
makedepends=(git boost sane autoconf-archive libxslt)
provides=(utsushi)
conflicts=(utsushi)
source=("git+https://gitlab.com/utsushi/utsushi.git")
sha256sums=('SKIP')

export CFLAGS+=" -Wno-error"
export CXXFLAGS="${CFLAGS}"

pkgver() {
  cd utsushi
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd utsushi
  # https://gitlab.com/utsushi/utsushi/-/issues/108
  sed -e 's|&& (SANE_MINOR == 0)||' -i sane/version.hpp # Fix build with sane 1.1
  ./bootstrap
}

build() {
  cd utsushi
  ./configure \
    --prefix=/usr/ \
    --libexecdir=/usr/lib/ \
    --sysconfdir=/etc/ \
    --enable-code-coverage \
    --enable-sane-config \
    --enable-udev-config \
    --with-gtkmm \
    --with-jpeg \
    --with-magick \
    --with-magick-pp \
    --with-sane \
    --with-tiff

  make
}

check() {
  cd utsushi
  make -k check || true
  make test || true
}

package() {
  cd utsushi
  make DESTDIR="${pkgdir}/" install
}
