# Maintainer: easymodo <easymodofrf@gmail.com>

pkgname=qimgv-git
pkgver=0.8.8.r12.g6e84931
pkgrel=1
pkgdesc="Simple image viewer written in qt. Supports webm playback."
arch=(x86_64 i686 armv6h armv7h aarch64)
url="http://github.com/easymodo/qimgv"
license=(GPL3)
depends=(qt5-base qt5-imageformats qt5-svg mpv exiv2)
makedepends=(git cmake qt5-tools)
optdepednds=('qt5-apng-plugin: animated png support'
             'qtraw: raw images support')
provides=(qimgv)
conflicts=(qimgv)
source=("git+https://github.com/easymodo/qimgv.git")
md5sums=('SKIP')

pkgver() {
    cd "${pkgname/-git/}"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/v//g'
}

prepare() {
    cd "${pkgname/-git/}"
    install -d build
}

build() {
    cd "${pkgname/-git/}/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd "${pkgname/-git/}/build"
    make DESTDIR=${pkgdir} install
}
