# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
 
pkgname=maui-pix-git
pkgver=2.0.0.r2.g0e855ae
pkgrel=1
pkgdesc='Image gallery for Maui'
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/mauikit/pix"
license=(GPL3)
groups=(maui)
depends=(mauikit mauikit-imagetools mauikit-filebrowsing)
makedepends=(git cmake extra-cmake-modules)
provides=(maui-pix)
conflicts=(maui-pix pix)
source=("git+https://invent.kde.org/kde/maui-pix.git")
sha256sums=('SKIP')

pkgver() {
  cd "maui-pix"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "maui-pix"
  install -d build
}

build() {
  cd "maui-pix/build"
  cmake .. 
  make
}
 
package() {
  cd "maui-pix/build"
  make DESTDIR="$pkgdir" install
}
