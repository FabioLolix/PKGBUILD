# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
 
pkgname=maui-pix-git
pkgver=r345.1d8fbab
pkgrel=1
pkgdesc='Image gallery for Maui'
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/mauikit/pix"
license=(GPL3)
depends=(mauikit-git)
makedepends=(git)
provides=(maui-pix)
conflicts=(maui-pix pix)
source=("git+https://invent.kde.org/kde/maui-pix.git")
sha256sums=('SKIP')

pkgver() {
  cd "maui-pix"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "maui-pix"
  install -d build
}

build() {
  cd "maui-pix/build"
  cmake .. 
  make
}
 
package() {
  cd "maui-pix/build"
  make DESTDIR="$pkgdir" install
}
