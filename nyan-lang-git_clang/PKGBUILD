# Maintainer: Eric Bailey <nerflad@gmail.com>

pkgname=nyan-lang-git_clang
pkgver=0.3.1.r2.g12f2735
pkgrel=1
pkgdesc="A data description language designed for openage"
arch=(x86_64 i686 pentium4 armv7h aarch64)
url="https://github.com/SFTtech/nyan"
license=(LGPL-3.0-or-later)
depends=(glibc gcc-libs)
makedepends=(cmake git clang)
#provides=(nyan-lang) # can't mix https://github.com/SFTtech/openage/issues/1727#issuecomment-2525188779
conflicts=(nyan-lang)
source=("nyan-lang::git+https://github.com/SFTtech/nyan.git")
sha256sums=('SKIP')

pkgver() {
  cd nyan-lang
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export CC=/usr/bin/clang CXX=/usr/bin/clang++
  cmake -B build -S "nyan-lang" -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
