# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=cawbird-git
pkgver=1.1.0.2.r49.ga9b96ae9
pkgrel=1
pkgdesc="Native Gtk+ Twitter Client"
arch=(i686 x86_64)
license=(GPL)
url="https://ibboard.co.uk/cawbird/"
depends=(gtk3 sqlite3 gspell
         gst-plugins-good gst-plugins-bad gst-libav gst-plugin-gtk)
makedepends=(vala git meson)
provides=(cawbird)
conflicts=(cawbird)
source=("git+https://github.com/IBBoard/cawbird.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  meson _build --prefix=/usr
  ninja -C _build
}

package() {
  cd "${pkgname%-git}"
  DESTDIR="${pkgdir}" ninja -C _build install
}
