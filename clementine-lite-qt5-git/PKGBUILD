# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
#
# Thanks to all the people of clementine-git and clementine-qt5-git:
# Contributor: Adria Arrufat <swiftscythe@gmail.com>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Daniel Hill <daniel.hill@orcon.net.nz>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Eduardo Sánchez Muñoz
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine-qt5-lite-git
pkgver=1.3.1.r776.g88131ec5f
pkgrel=1
pkgdesc="A modern music player and library organiser, qt5 version, disabled plugins"
arch=('i686' 'x86_64')
url='http://www.clementine-player.org/'
license=('GPL')
depends=('chromaprint'
 		 'glew' 
         'gst-plugins-base'
		 'hicolor-icon-theme'
		 'libcdio' 
		 'libplist' 
		 'protobuf'
		 'qt5-base'
         'crypto++'
         'taglib')
optdepends=('gst-plugins-good: for use with "Good" plugin libraries'
            'gst-plugins-bad: for use with "Bad" plugin libraries'
            'gst-plugins-ugly: for use with "Ugly" plugin libraries'
            'gst-libav: Libav plugin'
            'gvfs: Various devices support')
#optdepends+=(
#'sparsehash: required by Drive, Skydrive, Dropbox, Box, Seafile'
#'liblastfm: for lastfm support'
#'libmygpo-qt: for gpodder.net'
#'libspotify: for Spotify support'
#'libgpod: for iPod support'
#'libimobiledevice: for iPhone and iPod Touch support'
#'libmtp: for MTP devices, Android'
#)
makedepends=(git boost cmake mesa qt5-tools qt5-x11extras)
provides=(clementine)
conflicts=(clementine)
source=("clementine::git+https://github.com/clementine-player/Clementine.git#branch=qt5")
sha256sums=('SKIP')

pkgver() {
  cd "clementine"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/clementine/bin"
    
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Debug \
        -DENABLE_VISUALISATIONS=OFF \
        -DENABLE_SPOTIFY_BLOB=OFF \
        -DENABLE_SKYDRIVE=OFF \
        -DENABLE_GOOGLE_DRIVE=OFF \
        -DENABLE_DROPBOX=OFF \
        -DENABLE_SEAFILE=OFF \
        -DENABLE_LIBGPOD=OFF \
        -DENABLE_WIIMOTEDEV=OFF \
        -DENABLE_SPARKLE=OFF \
        -DENABLE_LIBMTP=OFF \
        -DENABLE_GIO=ON \
        -DENABLE_BOX=OFF \
		-DENABLE_LIBLASTFM=OFF
make
# -DENABLE_xxxx
# - force their inclusion with =ON  - this could trigger an error for
#   unlisted dependencies in depends=()
# - remove them to get auto-detected - this could include (without notice)
#   packages not listed in depends=()
# -DCMAKE_BUILD_TYPE remove to get a debug build with symbols
# Qt doesn't respect LC_xxx settings... which is really annoying! 
}

package() {
    cd "$srcdir/clementine/bin"
    make DESTDIR="$pkgdir" install
}
