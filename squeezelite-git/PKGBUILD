# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Christian Hesse <mail@eworm.de
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>

pkgname=squeezelite-git
pkgver=2.0.0.1541.r525.g72e1fd8
pkgrel=1
pkgdesc="Lightweight headless squeezebox emulator"
arch=(x86_64 i686 armv7h aarch64)
url="https://github.com/ralph-irving/squeezelite"
license=(GPL-3.0-or-later)
depends=(glibc alsa-lib faad2 flac libmad libvorbis mpg123 libsoxr ffmpeg libavformat.so)
makedepends=(git)
provides=(squeezelite)
conflicts=(squeezelite)
#install=squeezelite.install
source=("git+https://github.com/ralph-irving/squeezelite.git"
        "squeezelite-rpms::git+https://src.fedoraproject.org/rpms/squeezelite.git"
        squeezelite.system.service
        squeezelite.user.service
        squeezelite.sysconfig
        'conffile')
sha256sums=('SKIP'
            'SKIP'
            '411c102fad3d94346bf73cf6e06b6aa0bb7eb9345028c77cdb26fd3914e04740'
            '78b1b4b5a911bea17c98948d7a3dada3f0372a26e936b5bd7e2956e3c2d5e2f9'
            'ad2b29085c77a45b315ee48e8ee0b18b4327e82d2384644cb2c58be7877e005e'
            'f0753a1cbd0194119226587ff9c12257438674d9b8e0179d22f0d5461ad3a70a')

pkgver() {
  cd "${srcdir}/squeezelite"

  _maj=$(cat squeezelite.h|grep "#define MAJOR_VERSION" | awk '{print $3}' | sed 's/\"//g;s/-/_/g')
  _min=$(cat squeezelite.h|grep "#define MINOR_VERSION" | awk '{print $3}' | sed 's/\"//g;s/-/_/g')
  _mic=$(cat squeezelite.h|grep "#define MICRO_VERSION" | awk '{print $3}' | sed 's/\"//g;s/-/_/g')

  printf "%s.r%s.g%s"  "${_maj}.${_min}.${_mic}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/squeezelite"
  export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
  export OPTS="${OPTS} -DDSD -DRESAMPLE -DVISEXPORT -DFFMPEG -DLINKALL"
  make
}

package() {
  cd "${srcdir}/squeezelite"
  install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
  #install -Dm644 ../conffile "${pkgdir}/etc/squeezelite.conf.default"
  install -Dm644 ../squeezelite.sysconfig "${pkgdir}/etc/squeezelite.conf.default"
  install -Dm644 ../squeezelite.system.service -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 ../squeezelite.user.service   -t "${pkgdir}/usr/lib/systemd/user/"
  install -Dm644 doc/squeezelite.1 -t "${pkgdir}/usr/share/man/man1/"
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

