# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Alexey Yakovenko <waker@users.sourceforge.net>

pkgname=deadbeef
pkgver=1.8.7
pkgrel=1
pkgdesc="Modular GTK audio player for GNU/Linux"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://deadbeef.sourceforge.io/"
license=(GPL2 LGPL2.1 ZLIB)
depends=(gtk3 alsa-lib jansson libblocksruntime libdispatch)
makedepends=(libvorbis libmad flac curl imlib2 wavpack libsndfile libcdio libcddb
             libx11 faad2 zlib intltool pkgconfig libpulse libzip libsamplerate
             yasm ffmpeg clang)
optdepends=('alsa-oss: for OSS output plugin'
            'cdparanoia: for cd audio plugin'
            'curl: for last.fm, vfs_curl (shoutcast/icecast), artwork plugins'
            'dbus: for notification daemon support (OSD current song notifications)'
            'faad2: for AAC plugin'
            'ffmpeg: for ffmpeg plugin'
            'flac: for flac plugin'
            'imlib2: for artwork plugin'
            'libcddb: for cd audio plugin'
            'libcdio: for cd audio plugin'
            'libice: optional dependency for gtkui session client support'
            'libmad: for mp3 plugin (mpeg1,2 layers1,2,3)'
            'libogg: for ogg vorbis plugin'
            'libsamplerate: for dsp_libsrc plugin (resampler)'
            'libsidplay: for SID player plugin'
            'libsm: optional dependency for gtkui session client support'
            'libsndfile: for sndfile plugin'
            'libvorbis: for ogg vorbis plugin'
            'libx11: for global hotkeys plugin'
            'libzip: for vfs_zip plugin'
            'mpg123: for MP1/MP2/MP3 playback'
            'opusfile: for opus plugin'
            'pulseaudio: for PulseAudio output plugin'
            'wavpack: for wavpack plugin'
            'yasm: required to build assembly portions of ffap plugin'
            'zlib: for Audio Overload plugin (psf, psf2, etc), GME (for vgz)')
_apbuildcommit=c64f490e54447e3902fae54784d01ab59fa7dc95
_mp4pcommit=18b9197077012ea077be67baa4505fccb20552b6
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/DeaDBeeF-Player/${pkgname}/archive/${pkgver}.tar.gz"
        "apbuild-${pkgver}-${_apbuildcommit}.tar.gz::https://github.com/DeaDBeeF-Player/apbuild/archive/${_apbuildcommit}.tar.gz"
        "mp4p-${pkgver}-${_mp4pcommit}.tar.gz::https://github.com/DeaDBeeF-Player/mp4p/archive/${_mp4pcommit}.tar.gz")
sha512sums=('b8026b934a211ed24cdbc7e738fbfaa95d20706c0fcaef65530d7354e19d71c1e05af59305354478b4c4f1dbe4f7dbf436af7093d25727a2508b24c692a09258'
            '33b293a5b494ecfb50a48a37aca95de054125e2e864d197bd374bd1477bfcf1a8f5bc924303a7a846c1971c6503dbf0e82e74ff6db605a7720736a5696acdeda'
            '7aad33096f2cdda72136fa58a9164d8665c96b312c5bab954b4705fb122c21b02db31f651d322dda8881c87abefe0a3f0bd49d434fc7aa148de47974a00eb805')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rmdir external/{apbuild,mp4p}
  ln -s "${srcdir}"/apbuild-$_apbuildcommit external/apbuild
  ln -s "${srcdir}"/mp4p-$_mp4pcommit external/mp4p

  ./autogen.sh
}

build () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CC=clang CXX=clang++ ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
