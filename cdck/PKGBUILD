# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=cdck
pkgver=0.7.0
pkgrel=1
pkgdesc="CD/DVD check tools"
arch=(x86_64 i686)
url="http://swaj.net/unix/index.html#cdck"
license=(GPL2)
depends=(gcc-libs)
optdepends=(gnuplot)
source=("http://swaj.net/unix/cdck/${pkgname}-${pkgver}.tar.gz"
        "http://deb.debian.org/debian/pool/main/c/cdck/${pkgname}_${pkgver}+dfsg-3.debian.tar.xz")
sha256sums=('cb817ef2e8ab37d499d3630264901b9d22ebe67345507acd364f8f1e3995b404'
            '58e5ff1055fece7cc2482796d766d0e68caced0ed4522510e25b99cf84b8376f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for patch in ../debian/patches/*.patch; do 
    patch -Np1 < $patch
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/lib/libcdck.a
}
