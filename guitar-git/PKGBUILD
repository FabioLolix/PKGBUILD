# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=guitar-git
pkgver=1.2.0.r89.56c17eb
pkgrel=1
pkgdesc="Open source Git GUI Client"
arch=(x86_64 i686 armv6h armv7h aarch64)
url="https://soramimi.github.io/Guitar/"
license=(GPL)
depends=(qt5-svg git file)
makedepends=(cmake qt5-tools git)
provides=(guitar)
conflicts=(guitar)
source=("${pkgname%-git}::git+https://github.com/soramimi/Guitar.git"
        "https://raw.githubusercontent.com/FabioLolix/AUR-artifacts/master/guitar-modify-filetype.h-include.patch")
sha256sums=('SKIP'
            'fa7fa997d15202ef002694040b5d9e70111ac53f0df6196e8e05dea8dd26a483')

pkgver() {
  cd "${pkgname%-git}"
  #printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

  TAG=$(git tag --list | tail -n 1 | sed 's/^v//')
  printf "${TAG}.r%s.%s" "$(git rev-list --count v${TAG}..HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  mkdir -p build
  patch -Np1 -i ../guitar-modify-filetype.h-include.patch
}

build() {
  cd "${pkgname%-git}/build"
  export CC=clang CXX=clang++
  cmake ..
  make
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "build/Guitar" "${pkgdir}/usr/bin/guitar"
  install -D "LinuxDesktop/Guitar.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -D "LinuxDesktop/guitar.desktop" -t "${pkgdir}/usr/share/applications/"
}
