# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Alejandro Valdes <alejandrovaldes@live.com>
# Contributor: liberodark

pkgname=guitar-git
pkgver=v1.1.1.r29.ge49e921
pkgrel=1
pkgdesc="Open source Git GUI Client"
arch=(x86_64 i686 armv6h armv7h aarch64)
url="https://soramimi.github.io/Guitar/"
license=(GPL)
depends=(qt5-svg git file)
makedepends=(ruby cmake qt5-tools git)
source=("${pkgname%-git}::git+https://github.com/soramimi/Guitar.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  ruby prepare.rb
  mkdir -p build
}

build() {
  cd "${pkgname%-git}/build"
  qmake ../Guitar.pro
  make
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "_bin/Guitar" "$pkgdir/usr/bin/$pkgname"
  install -D "LinuxDesktop/Guitar.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -D "LinuxDesktop/guitar.desktop" "$pkgdir/usr/share/applications/${pkgname}.desktop"
}
