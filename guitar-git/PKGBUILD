# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=guitar-git
pkgver=1.2.0.r400.ga1e4055
pkgrel=1
pkgdesc="Open source Git GUI Client"
arch=(x86_64 i686 armv7h aarch64)
url="https://soramimi.github.io/Guitar/"
license=(GPL)
depends=(qt6-svg git file)
makedepends=(cmake qt6-tools qscintilla-qt6 git qt6-5compat)
provides=(guitar)
conflicts=(guitar)
source=("git+https://github.com/soramimi/Guitar.git")
sha256sums=('SKIP')

pkgver() {
  cd "Guitar"
  git describe --long --tags --exclude continuous | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "Guitar"
  mkdir -p build
}

build() {
  cd "Guitar/build"
  cmake -Wno-dev .. -DBUILD_WITH_QT6=ON
  make
}

package() {
  cd "Guitar"
  install -Dm755 "build/Guitar" "${pkgdir}/usr/bin/guitar"
  install -D "LinuxDesktop/Guitar.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -D "LinuxDesktop/guitar.desktop" -t "${pkgdir}/usr/share/applications/"
}
