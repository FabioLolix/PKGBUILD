# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Maintainer: E5ten

pkgname=lumina-pdf-git
pkgver=r19.ab86997
pkgrel=1
pkgdesc="PDF viewer for the lumina desktop environment"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://lumina-desktop.org/"
license=(BSD)
depends=(qt5-base)
makedepends=(git qt5-svg qt5-tools poppler-qt5)
source=(git+https://github.com/lumina-desktop/lumina-pdf.git)
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build(){
  cd "${pkgname%-git}/src-qt5/"
  qmake QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" LIBPREFIX=/usr/lib QT5LIBDIR=/usr/lib/qt CONFIG+=WITH_I18N L_MANDIR=/usr/share/man L_ETCDIR="/etc"
  make
}

package() {
  cd "${pkgname%-git}/src-qt5/"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm 644 "${srcdir}/${pkgname%-git}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname%-git}/LICENSE"
}
