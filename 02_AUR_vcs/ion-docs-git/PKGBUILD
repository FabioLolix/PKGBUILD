# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=ion-docs-git
pkgver=1.0.5.r1380.ga5fb10c7
pkgrel=1
pkgdesc="The Ion Shell, compatible with Redox and Linux (with docs)"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/redox-os/ion"
license=(MIT)
makedepends=(git mdbook rust)
provides=(ion)
conflicts=(ion)
install=${pkgname}.install
source=("git+https://github.com/redox-os/ion.git"
        "$pkgname-LICENSE::https://raw.githubusercontent.com/redox-os/ion/master/LICENSE")
sha512sums=('SKIP'
            '373cd0dd73c29aee38cb3bb8046345f19776c8047342fff1a055831d027cba6348a9ba23eb389883837048fe32d31461f66e266bed4a18849dd13ffd67f32ae8')

pkgver() {
  cd "$srcdir/${pkgname/-docs-git/}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname/-docs-git/}"
  cargo build --release

  cd "$srcdir/${pkgname/-docs-git/}/manual"
  mdbook build
}

check() {
  cd "$srcdir/${pkgname/-docs-git/}"
  cargo test
}

package() {
  cd "$srcdir/${pkgname/-docs-git/}"
  install -Dm755 target/release/ion "$pkgdir/usr/bin/ion"

  install -Dm644 "${srcdir}/$pkgname-LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/MIT"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r "${srcdir}/${pkgname/-docs-git/}/manual/book" "${pkgdir}/usr/share/doc/${pkgname}"
}
