# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Maintainer: Caleb Bassi <calebjbassi@gmail.com> -> also the program developer

pkgname=gotop
pkgver=3.0.0
pkgrel=5
pkgdesc='A terminal based graphical activity monitor inspired by gtop and vtop'
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/cjbassi/gotop"
license=(AGPL3)
makedepends=(go git)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cjbassi/gotop/archive/${pkgver}.tar.gz")
sha256sums=('d5147080bb6057f0bf0900b38438e89aa066959c845bdd4c84a9c9fe478b176f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  install -m755 -d "$srcdir/go/src/github.com/cjbassi"
  cp -a "$srcdir/$pkgname-$pkgver" "$srcdir/go/src/github.com/cjbassi/gotop"
}

build() {
  cd "$srcdir/go/src/github.com/cjbassi/gotop"

  export GOPATH="$srcdir/go"
  export GOFLAGS="-gcflags=all=-trimpath=${PWD} -asmflags=all=-trimpath=${PWD} -ldflags=-extldflags=-zrelro -ldflags=-extldflags=-znow -buildmode=pie"

  go get -v github.com/cjbassi/gotop
  make dist/gotop
}

package() {
  install -Dm755 "$srcdir"/go/bin/gotop "$pkgdir"/usr/bin/gotop
}
