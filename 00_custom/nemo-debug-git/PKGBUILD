# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo-debug-git
pkgver=4.6.5.r0.g4d3a935d
pkgrel=1
pkgdesc="Cinnamon file manager, git-version"
arch=(i686 x86_64)
url="https://github.com/linuxmint/nemo"
license=(GPL)
depends=(cinnamon-desktop dconf exempi gvfs libexif
         libnotify libxml2 python xapps)
makedepends=(git gobject-introspection intltool meson)
optdepends=('cinnamon-translations: i18n'
            'ffmpegthumbnailer: support for video thumbnails')
conflicts=(nemo)
provides=(nemo)
options=(!emptydirs !strip debug)
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd nemo
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd nemo
  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i '/^\[Desktop Entry/,/^\[Desktop Action/ s/^Name\(.*\)=.*/Name\1=Nemo/' \
  data/nemo.desktop.in
  install -d "${srcdir}/nemo/build"
}

build() {
  cd "${srcdir}/nemo/build"
  meson .. \
    --prefix=/usr \
    --libexecdir=lib/nemo \
    --buildtype=debug
  ninja
}

package() {
  cd "${srcdir}/nemo/build"
  DESTDIR="${pkgdir}" ninja install
}
