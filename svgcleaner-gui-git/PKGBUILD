# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=svgcleaner-gui-git
pkgver=0.9.5.r1.gdfd1fd8
pkgrel=1
pkgdesc="main GUI for svgcleaner"
url="https://github.com/RazrFalcon/svgcleaner-gui"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
license=(GPL2)
depends=(qt5-base svgcleaner)
makedepends=(git qt5-tools qt5-svg)
source=(git+https://github.com/RazrFalcon/svgcleaner-gui)
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  qmake-qt5 svgcleaner-gui.pro
  make 
}

package() {
  cd "${pkgname%-git}"
  make "INSTALL_ROOT=$pkgdir" install
}
