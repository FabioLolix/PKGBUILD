# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=writer-git
pkgver=r241.410d972
pkgrel=1
pkgdesc="Word processor built with Vala and GTK+, especially for elementary OS"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/ryonakano/writer"
license=(GPL3)
depends=(libgranite.so gtksourceview4)
makedepends=(git vala meson)
provides=(writer)
conflicts=(writer)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
  cd "${pkgname%-git}"
  meson . build --prefix=/usr
  ninja -C build
}

package() {
  cd "${pkgname%-git}"
  DESTDIR="${pkgdir}" ninja -C build install
  ln -s /usr/bin/com.github.ryonakano.writer "$pkgdir/usr/bin/writer"
}
