# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
#
# Thanks to all the people of clementine-git and clementine-qt5-git
# Contributor: Adria Arrufat <swiftscythe@gmail.com>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Daniel Hill <daniel.hill@orcon.net.nz>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Eduardo Sánchez Muñoz
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine-lite-git
pkgver=1.4.0rc1.r596.g590bcf1c7.0.g590bcf1c7
pkgrel=1
pkgdesc="A modern music player and library organiser, disabled plugins"
arch=(i686 x86_64 aarch64)
url='http://www.clementine-player.org/'
license=(GPL)
depends=(qt5-base qt5-x11extras chromaprint taglib protobuf libcdio gst-plugins-base fftw)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-good: additional codecs'
            'gst-plugins-ugly: additional codecs')
makedepends=(git boost cmake mesa qt5-tools glu)
provides=(clementine)
conflicts=(clementine)
source=("clementine::git+https://github.com/clementine-player/Clementine.git")
sha256sums=('SKIP')

pkgver() {
  cd "clementine"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/clementine/bin"
    
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_VISUALISATIONS=OFF \
    -DUSE_SYSTEM_TAGLIB=ON \
    -DENABLE_GIO=ON \
    -DENABLE_MOODBAR=ON \
    -DINSTALL_UBUNTU_ICONS=ON \
    -DENABLE_SPOTIFY=OFF \
    -DENABLE_SPOTIFY_BLOB=OFF \
    -DENABLE_SKYDRIVE=OFF \
    -DENABLE_GOOGLE_DRIVE=OFF \
    -DENABLE_DROPBOX=OFF \
    -DENABLE_SEAFILE=OFF \
    -DENABLE_LIBGPOD=OFF \
    -DENABLE_WIIMOTEDEV=OFF \
    -DENABLE_SPARKLE=OFF \
    -DENABLE_LIBMTP=OFF \
    -DENABLE_BOX=OFF \
    -DENABLE_LIBLASTFM=OFF
  make
}

package() {
    cd "$srcdir/clementine/bin"
    make DESTDIR="$pkgdir" install
}
