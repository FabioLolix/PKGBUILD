# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Robert F. Nash

pkgname=perl-webservice-musicbrainz-git
_pkgname=WebService-MusicBrainz
pkgver=r67.d967eae
pkgrel=1.1
pkgdesc="Web service API to MusicBrainz database"
arch=(any)
url="https://metacpan.org/release/WebService-MusicBrainz"
license=(PerlArtistic)
depends=(perl-mojolicious) #perl-class-accessor perl-uri perl-xml-libxml perl-libwww 
makedepends=(git )
checkdepends=(perl-io-socket-ssl)
provides=(perl-webservice-musicbrainz)
conflicts=(perl-webservice-musicbrainz)
options=(!emptydirs)
source=("perl-webservice-musicbrainz::git+https://github.com/bfaist/webservice-musicbrainz.git")
sha512sums=('SKIP')

pkgver() {
  cd perl-webservice-musicbrainz
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd perl-webservice-musicbrainz
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd perl-webservice-musicbrainz
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test || :
}

package() {
  cd perl-webservice-musicbrainz
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}