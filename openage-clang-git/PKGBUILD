# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: fzerorubigd <fzero@rubi.gd>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=openage-clang-git
pkgver=0.4.0.r14.g592731f66
pkgrel=1
pkgdesc="A free (as in freedom) clone of the Age of Empires II engine"
arch=(i686 x86_64)
url="http://openage.sft.mx/"
license=(GPL3)
depends=(libepoxy ftgl sdl2_image opusfile eigen
         qt5-declarative xdg-utils sdl2 qt5-quickcontrols
         libpng libogg python-pillow python-numpy 
         python-pygments python-jinja nyan-lang-git)
makedepends=(git cmake cython doxygen vulkan-headers clang)
optdepends=('ttf-dejavu: for text display, but you can use any font'
            vulkan-icd-loader)
provides=(openage)
conflicts=(openage)
source=("git+https://github.com/SFTtech/openage")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-clang-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare() {
  cd "$srcdir/${pkgname%-clang-git}"
  mkdir -p build
}
build() {
  cd "$srcdir/${pkgname%-clang-git}/build"

  export CC=/usr/bin/clang CXX=/usr/bin/clang++

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=Release ..
  make
}
package() {
  cd "$srcdir/${pkgname%-clang-git}/build"
  make DESTDIR="$pkgdir/" install
}
