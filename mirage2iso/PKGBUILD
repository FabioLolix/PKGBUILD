# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: ezzetabi <ezzetabi@gawab.com>
# Contributor: Sebastian Schwarz <seschwar@gmail.com>

pkgname=mirage2iso
pkgver=0.4.2
pkgrel=3
pkgdesc="libmirage-based CD/DVD image file converter"
arch=(i686 x86_64)
url="https://github.com/mgorny/mirage2iso"
license=(BSD-3-Clause)
depends=(libassuan libmirage)
source=("https://github.com/mgorny/mirage2iso/releases/download/v${pkgver}/mirage2iso-${pkgver}.tar.xz"
        "https://github.com/mgorny/mirage2iso/releases/download/v${pkgver}/mirage2iso-${pkgver}-tests.tar.xz")
noextract=(mirage2iso-${pkgver}-tests.tar.xz)
sha256sums=('f92af1737a7d5756a76d6ccf4a2b7ed922d271eb0ce01e4e32efc5516638d30d'
            '2823fde4cb108e1bc95e2e3d3b4612e5d3a8d51825b7dbc1a556177eeb7bb340')

prepare() {
  cd "mirage2iso-${pkgver}"
  bsdtar xf "${srcdir}/mirage2iso-${pkgver}-tests.tar.xz" --strip-components 1
}

build() {
  cd "mirage2iso-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "mirage2iso-${pkgver}"
  make -k check || :
  # https://github.com/projg2/mirage2iso/issues/6
}

package() {
  cd "mirage2iso-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt"
}
