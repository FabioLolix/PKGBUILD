# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: jiribb <jiribb@gmail.com>
# Contributor: David Spicer <azleifel at googlemail dot com>
# Contributor: Andrew Brouwers
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=handbrake-git
pkgname=('handbrake-git' 'handbrake-cli-git')
pkgver=1.1.0.r493.gbd255afee
pkgrel=1
pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter"
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
source=("${pkgname}::git+https://github.com/HandBrake/HandBrake.git"
	'https://download.handbrake.fr/handbrake/contrib/ffmpeg-4.0.1.tar.bz2'
	'https://download.handbrake.fr/handbrake/contrib/libbluray-1.0.2.tar.bz2'
	'https://download.handbrake.fr/handbrake/contrib/libdvdnav-5.0.3.tar.bz2'
	'https://download.handbrake.fr/handbrake/contrib/libdvdread-5.0.3.tar.bz2'
	'https://download.handbrake.fr/handbrake/contrib/libvpx-1.7.0.tar.gz'
	'https://download.handbrake.fr/handbrake/contrib/mfx_dispatch-5d6f1b6.tar.gz'
	'https://download.handbrake.fr/handbrake/contrib/nv-codec-headers-8.1.24.2.tar.gz'
	'https://download.handbrake.fr/handbrake/contrib/x265_2.8.tar.gz')
makedepends=('git' 'cmake' 'intltool' 'python2' 'gettext' 'nasm' 'paxtest' 'lame' 'opus' 'jansson' 'libvorbis' 'libx264' 'libass' 'gtk3' 'libsamplerate' 'libtheora' libva speex)
optdepends=("intel-media-server-studio: for QSV" "nvidia-utils: for nvenc")
sha256sums=('SKIP'
            '7ee591b1e7fb66f055fa514fbd5d98e092ddb3dbe37d2e50ea5c16ab51c21670'
            '6d9e7c4e416f664c330d9fa5a05ad79a3fb39b95adfc3fd6910cbed503b7aeff'
            '5097023e3d2b36944c763f1df707ee06b19dc639b2b68fb30113a5f2cbf60b6d'
            '321cdf2dbdc83c96572bc583cd27d8c660ddb540ff16672ecb28607d018ed82b'
            '1fec931eb5c94279ad219a5b6e0202358e94a93a90cfb1603578c326abfc1238'
            '45d8ae5e525a51bfcb924c91b7153b5f0899c94d87d933913e4297b2ae7a3f8e'
            '2b339b2493f5b63b285dd660be05102af7d4ff2de9fcf3af94e48b5f91a3ca57'
            '6e59f9afc0c2b87a46f98e33b5159d56ffb3558a49d8e3d79cb7fdc6b7aaa863')
noextract=(
    'ffmpeg-4.0.1.tar.bz2'
    'libbluray-1.0.2.tar.bz2'
    'libdvdnav-5.0.3.tar.bz2'
    'libdvdread-5.0.3.tar.bz2'
    'libvpx-1.7.0.tar.gz'
    'mfx_dispatch-5d6f1b6.tar.gz'
    'nv-codec-headers-8.1.24.2.tar.gz'
    'x265_2.8.tar.gz'
    )

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"

  # python2 substitutions
  sed -i -e '1c#! /usr/bin/python2' "gtk/src/makedeps.py"
  sed -i -e '1c#! /usr/bin/python2' "make/configure.py"
  
  [ -d download ] || mkdir download
  for _tarball in ${noextract[@]}; do
    cp ../${_tarball} download/
  done
}

build() {
  cd "$srcdir/$pkgname"

  ./configure --launch --build="build" --prefix=/usr \
	      --disable-gtk-update-checks --launch-jobs=0 \
	      --enable-nvenc --enable-qsv --force

  # Create build-specific temporary directory for ffmpeg
  install -d -m755 "build/build_tmp"

  make -C "build" TMPDIR="$srcdir/$pkgname/build/build_tmp"
  /usr/lib/paxtest/execstack -c "$srcdir/$pkgname/build/HandBrakeCLI"
  /usr/lib/paxtest/execstack -c "$srcdir/$pkgname/build/gtk/src/ghb"
}

package_handbrake-git() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (GUI version)"
  depends=('opus' 'jansson' 'libsamplerate' 'libx264' 'libtheora' 'lame' 'libass' 'gtk3'
	   'dbus-glib' 'gst-plugins-base-libs' 'libgudev' 'libnotify')
  optdepends+=('gst-plugins-bad: For Preview Window'
              'gst-plugins-good: For Preview Window'
              'gst-plugins-ugly: For Preview Window'
              'gst-libav: For Preview Window')
  provides=('handbrake-gtk' 'handbrake')
  conflicts=('handbrake-gtk' 'handbrake')

  cd "$srcdir/$pkgname"
  make -C "build" DESTDIR="$pkgdir/" install

  rm -f "$pkgdir/usr/bin/HandBrakeCLI"

  # icon-theme.cache will be rebuilt on install
  rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}

package_handbrake-cli-git() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (CLI version)"
  depends=('opus' 'jansson' 'libsamplerate' 'libx264' 'libtheora' 'lame' 'libass' 'libxml2')
  provides=('handbrake-cli')
  conflicts=('handbrake-cli')
  install -D -m755 "$srcdir/$pkgname/build/HandBrakeCLI" "$pkgdir/usr/bin/HandBrakeCLI"
}
