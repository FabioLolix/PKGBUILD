# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libnpupnp-git
pkgver=6.0.1.r0.g8433fd6
pkgrel=2
pkgdesc="C++ base UPnP library, derived from Portable UPnP, a.k.a libupnp"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64 i686 armv6h armv7h aarch64)
license=(BSD)
depends=(glibc gcc-libs curl expat libmicrohttpd)
makedepends=(git)
provides=(libnpupnp libnpupnp.so)
conflicts=(libnpupnp)
source=("libnpupnp::git+https://framagit.org/medoc92/npupnp.git")
sha256sums=('SKIP')

pkgver() {
  cd "libnpupnp"
  git describe --long --tags | sed 's/^libnpupnp.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "libnpupnp"
  ./autogen.sh
}

build(){
  cd "libnpupnp"
  ./configure --prefix=/usr
  make
}

package(){
  cd "libnpupnp"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
