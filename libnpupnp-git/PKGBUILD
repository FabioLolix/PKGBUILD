# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libnpupnp-git
pkgver=6.2.1.r15.g36c5da6
pkgrel=1
pkgdesc="C++ base UPnP library, derived from Portable UPnP, a.k.a libupnp"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64 i686 armv6h armv7h aarch64)
license=(BSD)
depends=(glibc gcc-libs curl expat libmicrohttpd)
makedepends=(git meson)
provides=(libnpupnp libnpupnp.so)
conflicts=(libnpupnp)
source=("libnpupnp::git+https://framagit.org/medoc92/npupnp.git")
sha256sums=('SKIP')

pkgver() {
  cd "libnpupnp"
  git describe --long --tags | sed 's/^libnpupnp.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  arch-meson libnpupnp build
  meson compile -C build
}

package(){
  meson install -C build --destdir "$pkgdir"
  install -Dm644 libnpupnp/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
