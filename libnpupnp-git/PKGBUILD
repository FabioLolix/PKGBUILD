# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=libnpupnp-git
pkgver=5.0.2.r15.gca82744
pkgrel=1
pkgdesc="C++ base UPnP library, derived from Portable UPnP, a.k.a libupnp"
url="https://www.lesbonscomptes.com//upmpdcli/libupnpp-refdoc/libupnpp-ctl.html"
arch=(x86_64 i686 armv6h armv7h aarch64)
license=(BSD)
depends=(expat libmicrohttpd)
makedepends=(git)
provides=(libnpupnp)
conflicts=(libnpupnp)
source=("${pkgname%-git}::git+https://framagit.org/medoc92/npupnp.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^libnpupnp.v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  ./autogen.sh
}

build(){
  cd "${pkgname%-git}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname%-git}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
