# Maintainer: Eric Bailey <nerflad@gmail.com>

pkgname=nyan-lang-git
pkgver=r191.2d6fba8
pkgrel=1
pkgdesc="A data description language designed for openage"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://github.com/SFTtech/nyan"
license=('LGPL3')
depends=('gcc-libs')
makedepends=('cmake' 'git')
source=("$pkgname::git+https://github.com/SFTtech/nyan")
md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgname"
    mkdir -p build
}

build() {
	cd "$srcdir/$pkgname/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib \
          -DCMAKE_BUILD_TYPE=Release ..
	make
}

package() {
	cd "$srcdir/$pkgname/build"
    make DESTDIR="$pkgdir/" install
}
